apply plugin: 'java-library'
apply plugin: 'maven-publish'

ext {
  checkerFrameworkVersion = '2.4.0'
  checkerFrameworkProcessors = [
      'org.checkerframework.checker.interning.InterningChecker',
      'org.checkerframework.checker.nullness.NullnessChecker',
      'org.checkerframework.checker.optional.OptionalChecker'
  ]
  checkerFrameworkSkipUses = [
      '^io\\.reactivex\\.'
  ]
}

group = 'rxbeans'
sourceCompatibility = 8
version = '0.1'

repositories {
    jcenter()
}

configurations {
  checkerFrameworkCheckerJar {
    description = 'the Checker Framework, including the Type Annotations compiler'
  }
  checkerFrameworkAnnotatedJDK {
    description = 'a copy of JDK classes with Checker Framework type qualifers inserted'
  }
}

dependencies {
  api 'io.reactivex.rxjava2:rxjava:2.1.10'
  api "org.checkerframework:checker-qual:$checkerFrameworkVersion"
  checkerFrameworkAnnotatedJDK "org.checkerframework:jdk8:$checkerFrameworkVersion"
  checkerFrameworkCheckerJar "org.checkerframework:checker:$checkerFrameworkVersion"
}

javadoc {
  options {
    links(
        'https://docs.oracle.com/javase/9/docs/api',
        'http://reactivex.io/RxJava/javadoc',
        'https://checkerframework.org/api'
    )
    addBooleanOption('javafx', true)
  }
}

publishing {
  publications {
    main(MavenPublication) {
      from components.java
    }
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs.addAll([
      '-processorpath', "${configurations.checkerFrameworkCheckerJar.asPath}".toString(),
      "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}".toString(),
      '-processor', rootProject.checkerFrameworkProcessors.join(','),
      "-AskipUses=${rootProject.checkerFrameworkSkipUses.join('|')}".toString()
  ])
}

defaultTasks 'assemble', 'javadoc'